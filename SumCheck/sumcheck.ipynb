{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "ad93127c",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "最高degree=5的多项式:  6*x*y^3 + 2*x*y^2*z^2 + 3*x*y*z + 6*y^2*z^2 - 2*y*z\n",
      "\n",
      "P * z =  6*x*y^3*z + 2*x*y^2*z^3 + 3*x*y*z^2 + 6*y^2*z^3 - 2*y*z^2\n",
      "----------\n",
      "最高degree=2的多项式: 4*x^2 + 8*x*z - 6*x + z^2 + 8\n",
      "P(1,2,3): 5\n",
      "----------\n",
      "-x^2 + 6*x*y + x*z + 6*x + y^2 + y*z - 2*y + z^3 - 6*z^2 + z\n",
      "{(2, 0, 0): 16, (1, 1, 0): 6, (1, 0, 1): 1, (1, 0, 0): 6, (0, 2, 0): 1, (0, 1, 1): 1, (0, 1, 0): 15, (0, 0, 3): 1, (0, 0, 2): 11, (0, 0, 1): 1}\n",
      "----------\n",
      "逆字典序排序: \n",
      "P =  z^3 - x^2 + 6*x*y + y^2 + x*z + y*z - 6*z^2 + 6*x - 2*y + z\n",
      "\n",
      "另一个例子,带有多线性多项式: \n",
      "x*y*z + 5*x*y + 7*y + 7\n"
     ]
    }
   ],
   "source": [
    "F = GF(17) #大小为17的有限域\n",
    "R.<x, y, z> = PolynomialRing(F, order=\"lex\") #定义了一个多项式环,字典序\n",
    "\n",
    "P = 6*x^3*z^3*y - 6*z^2 - x - 2*y + 6\n",
    "assert(P.degree() == 7) #检查最高次数的项\n",
    "\n",
    "P = R.random_element(degree=5) #生成一个总次数最高为5的随机多项式\n",
    "print(\"最高degree=5的多项式: \", P)\n",
    "\n",
    "print(\"\\nP * z = \", P * z)\n",
    "\n",
    "print(\"----------\")\n",
    "\n",
    "P = R.random_element(degree=2) #最高次数为2\n",
    "print(\"最高degree=2的多项式:\", P)\n",
    "print(\"P(1,2,3):\", P(1,2,3)) #将x=1,y=2,z=3带入运算\n",
    "\n",
    "print(\"----------\")\n",
    "\n",
    "#字典序排列,谁的x指数大谁排前面,相同再比较y然后z\n",
    "P = 6*x*y - 6*z^2 - x^2 + y^2 - 2*y + 6*x + z + z^3 + x*z + y*z\n",
    "print(P)\n",
    "print(P.dict()) #键值形式6xy:(1, 1, 0): 6\n",
    "\n",
    "print(\"----------\")\n",
    "print(\"逆字典序排序: \")\n",
    "# 我们可以按不同的顺序对其进行排序\n",
    "# 这里是degrevlex，即“次数逆字典序”\n",
    "# https://doc.sagemath.org/html/en/reference/polynomial_rings/sage/rings/polynomial/\n",
    "R.<x, y, z> = PolynomialRing(F, order=\"degrevlex\")\n",
    "\n",
    "#使用同一个多项式\n",
    "# P = 6*x*y - 6*z^2 - x^2 + y^2 - 2*y + 6*x + z + z^3 + x*z + y*z #乱序\n",
    "print(\"P = \", R(P)) #将P转换到这个新环中\n",
    "\n",
    "print(\"\\n另一个例子,带有多线性多项式: \")\n",
    "print(5*x*y + 7*y + x*y*z + 7)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "73f59da4",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[(0, 0, 0), (0, 0, 1), (0, 1, 0), (0, 1, 1), (1, 0, 0), (1, 0, 1), (1, 1, 0), (1, 1, 1)]\n",
      "P: 6*a*b*c + 5*a*b + 4*a*c + 2*b*c + 3*a + b + 7\n",
      "超立方体上多项式的和: 15\n"
     ]
    }
   ],
   "source": [
    "import itertools\n",
    "\n",
    "dimension = 3\n",
    "cube = list(itertools.product([0, 1], repeat=dimension))\n",
    "print(cube)\n",
    "\n",
    "F = GF(17)\n",
    "R.<a, b, c> = F[]\n",
    "\n",
    "#多线性\n",
    "P = 6*a*b*c + 5*a*b + 4*a*c + 3*a + 2*b*c + b + 7\n",
    "\n",
    "# 我们可以检查每个变量的最高次数\n",
    "assert(P.degrees() == (1,1,1))\n",
    "print(\"P:\", P)\n",
    "\n",
    "#我们可以在超立方体上对多项式进行求值\n",
    "S = sum([P(i) for i in cube]) #[(0, 0, 0), (0, 0, 1), (0, 1, 0), (0, 1, 1), (1, 0, 0), (1, 0, 1), (1, 1, 0), (1, 1, 1)]\n",
    "print(\"超立方体上多项式的和:\", S)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cdc37915",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P:  2*a*b + 4*a + 2*b + 3\n"
     ]
    }
   ],
   "source": [
    "#多线性插值MLE\n",
    "F = GF(101)\n",
    "R.<a,b> = F[] #多项式环\n",
    "\n",
    "def multilinear_lagrange_interpolation(points, values):\n",
    "    def basis_polynomial(point):\n",
    "        result = R(1) #1\n",
    "        result *= a if point[0] == 1 else (1 - a)\n",
    "        #如果当前点的第0位是1，就乘a,如果是0乘(1-a)\n",
    "        result *= b if point[1] == 1 else (1 - b)\n",
    "        #同理b\n",
    "        return result\n",
    "\n",
    "    interpolation_polynomial = R(0) # 0多项式\n",
    "\n",
    "    for i in range(len(points)):\n",
    "        #把每个点的值(value)乘以对应的基函数，然后加起来\n",
    "        interpolation_polynomial += F(values[i]) * basis_polynomial(points[i]) #F mod 101防止有大数\n",
    "\n",
    "    return interpolation_polynomial\n",
    "\n",
    "#在(0, 0), (0, 1), (1, 0), (1, 1)取特定值点(3,5,7,11)\n",
    "points = [(0, 0), (0, 1), (1, 0), (1, 1)]\n",
    "values = [3, 5, 7, 11]\n",
    "\n",
    "P = multilinear_lagrange_interpolation(points, values)\n",
    "print(\"P: \", P)\n",
    "\n",
    "assert(P(points[0]) == values[0])\n",
    "assert(P(points[1]) == values[1])\n",
    "assert(P(points[2]) == values[2])\n",
    "assert(P(points[3]) == values[3])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "321c5fae",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "P:  -47*x1*x2*x3 + 3*x1*x2 - 24*x1*x3 + 28*x2*x3 + 37*x1 - 33*x2 + 28*x3 - 18\n"
     ]
    }
   ],
   "source": [
    "#等式函数\n",
    "F=GF(101)\n",
    "R.<x1,x2,x3,y1,y2,y3>=F[]\n",
    "\n",
    "cube=[(0,0,0),(0,0,1),(0,1,0),(0,1,1),(1,0,0),(1,0,1),(1,1,0),(1,1,1)]\n",
    "\n",
    "def eq(y): #输入000-111\n",
    "    beta=F(1) #1\n",
    "    beta *=x1 *y[0]+(1-x1)*(1-y[0]) \n",
    "    beta *=x2 *y[1]+(1-x2)*(1-y[1])\n",
    "    beta *=x3 *y[2]+(1-x3)*(1-y[2])\n",
    "    return beta\n",
    "\n",
    "def mle(values):\n",
    "    return sum(values[i] * eq(c) for i,c in enumerate(cube))\n",
    "\n",
    "a = [83,10,50,5,19,23,90,75]\n",
    "P = mle(a)\n",
    "print(\"P: \", P)\n",
    "\n",
    "assert P.degrees() == (1, 1, 1, 0, 0, 0)\n",
    "\n",
    "assert P(x1=cube[0][0],x2=cube[0][1],x3=cube[0][2]) == a[0] #P(000)=81\n",
    "assert P(x1=cube[1][0],x2=cube[1][1],x3=cube[1][2]) == a[1]\n",
    "assert P(x1=cube[2][0],x2=cube[2][1],x3=cube[2][2]) == a[2]\n",
    "assert P(x1=cube[3][0],x2=cube[3][1],x3=cube[3][2]) == a[3]\n",
    "assert P(x1=cube[4][0],x2=cube[4][1],x3=cube[4][2]) == a[4]\n",
    "assert P(x1=cube[5][0],x2=cube[5][1],x3=cube[5][2]) == a[5]\n",
    "assert P(x1=cube[6][0],x2=cube[6][1],x3=cube[6][2]) == a[6]\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0da81503",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "H = 9\n",
      "g1 = 3*x + 3\n",
      "g1(0) + g1(1) = 9\n",
      "g2 = 32*x + 32\n",
      "g2(0) + g2(1) = 96 \n"
     ]
    }
   ],
   "source": [
    "#2个变量的sumcheck\n",
    "F=GF(101)\n",
    "\n",
    "#定义一个两个变量的多项式环\n",
    "R.<x1,x2>=F[]\n",
    "\n",
    "#定义一个单变量多项式环\n",
    "Rx.<x>=F[]\n",
    "\n",
    "#定义一个多元多线性多项式\n",
    "P=1+x2+x1+x1*x2\n",
    "\n",
    "#计算P在布尔超立方体上的总和\n",
    "H=P(0,0)+P(0,1)+P(1,0)+P(1,1)\n",
    "print(f\"H = {H}\")\n",
    "#证明者计算g1(x)=P(x,0)+P(x,1)\n",
    "g1=P(x1,0)+P(x1,1)\n",
    "\n",
    "#将g1转换为关于x的单变量多项式（仅为清晰起见）\n",
    "g1=Rx(g1(x,0))\n",
    "print(f\"g1 = {g1}\")\n",
    "\n",
    "# 验证器检查g1是否正确求和得到H\n",
    "print(f\"g1(0) + g1(1) = {g1(0)+g1(1)}\")\n",
    "assert g1(0)+g1(1)==H\n",
    "\n",
    "#验证者发送一个随即挑战值r1\n",
    "r1=31\n",
    "\n",
    "#证明者构造g2(x)=P(r1,x)\n",
    "g2=P(r1,x2)\n",
    "\n",
    "#将g2转换为关于x的单变量多项式（仅为清晰起见）\n",
    "g2=Rx(g2(0,x))\n",
    "print(f\"g2 = {g2}\")\n",
    "\n",
    "#验证器检查g2是否正确地与g1(r1)相等\n",
    "print(f\"g2(0) + g2(1) = {g2(0) + g2(1)} \")\n",
    "assert g2(0) + g2(1) == g1(r1)\n",
    "\n",
    "r2 = 43\n",
    "assert g2(r2) == P(r1, r2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "id": "26babda9",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "3个变量的sumcheck协议: \n",
      "H=31\n",
      "Step1: Prover定义求和多项式g1=34*x + 49\n",
      "Step2: Verifier验证g1(0)+g1(1)=31应该等于31\n",
      "Step3: Verifier选择一个随机点\n",
      "g2=94*x + 65\n",
      "g2(0)+g2(1)=22应该等于22\n",
      "g3=52*x + 53\n",
      "g3(0)+g3(1)=57应该等于57\n"
     ]
    }
   ],
   "source": [
    "print(\"3个变量的sumcheck协议: \")\n",
    "F=GF(101)\n",
    "R.<x1,x2,x3>=F[]\n",
    "Rx.<x>=F[]\n",
    "\n",
    "#定义一个随机多元多线性多项式\n",
    "P=75*x1*x2+3*x2*x3+43*x1*x3+10*x2+11*x3+1\n",
    "H=P(0,0,0)+P(0,0,1)+P(0,1,0)+P(0,1,1)+P(1,0,0)+P(1,0,1)+P(1,1,0)+P(1,1,1)\n",
    "print(f\"H={H}\")\n",
    "\n",
    "#Prover定义一个求和多项式g1\n",
    "g1=P(x1,0,0)+P(x1,0,1)+P(x1,1,0)+P(x1,1,1)\n",
    "g1=Rx(g1(x,0,0))\n",
    "print(f\"Step1: Prover定义求和多项式g1={g1}\")\n",
    "print(f\"Step2: Verifier验证g1(0)+g1(1)={g1(0)+g1(1)}应该等于{H}\")\n",
    "assert g1(0)+g1(1)==H\n",
    "assert g1.degree()<=1\n",
    "\n",
    "r1=20\n",
    "print(\"Step3: Verifier选择一个随机点\")\n",
    "g2=P(r1,x2,0)+P(r1,x2,1)\n",
    "g2=Rx(g2(0,x,0))\n",
    "print(f\"g2={g2}\")\n",
    "print(f\"g2(0)+g2(1)={g2(0)+g2(1)}应该等于{g1(r1)}\")\n",
    "assert g2(0)+g2(1)==g1(r1)\n",
    "assert g2.degree()<=1\n",
    "\n",
    "r2=30\n",
    "g3=P(r1,r2,x3)\n",
    "g3=Rx(g3(0,0,x))\n",
    "print(f\"g3={g3}\")\n",
    "print(f\"g3(0)+g3(1)={g3(0)+g3(1)}应该等于{g2(r2)}\")\n",
    "assert g3(0)+g3(1)==g2(r2)\n",
    "assert g3.degree()<=1\n",
    "\n",
    "r3=40\n",
    "assert g3(r3)==P(r1,r2,r3)\n",
    "\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.3",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
